plugins {
    id 'java'
    id 'org.gretty' version '5.0.0'
}

group = 'com.lukamaret'
version = '0.1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.mysql:mysql-connector-j:9.5.0'
    implementation 'org.eclipse.persistence:org.eclipse.persistence.jpa:5.0.0-B11'
    implementation 'org.eclipse.persistence:org.eclipse.persistence.jpa.modelgen.processor:5.0.0-B11'
    implementation 'io.github.cdimascio:dotenv-java:3.2.0'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'jakarta.platform:jakarta.jakartaee-web-api:11.0.0'
    implementation project(':common')
}

def envFile = file('../.env')
def envVars = [:]

if (envFile.exists()) {
    envFile.readLines().each { line ->
        if (line && !line.startsWith('#') && line.contains('=')) {
            def (key, value) = line.split('=', 2)
            envVars[key.trim()] = value.trim()
        }
    }
}

gretty {
    servletContainer = 'tomcat11'
    jvmArgs = envVars.collect { key, value -> "-D${key}=${value}" }
    scanInterval = 0
    httpPort = envVars.getOrDefault('SOM_ADDRESS_PORT', '8092') as Integer
}
